{
  parserClass="com.example.sumlang.SumParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Sum"
  psiImplClassSuffix="Impl"
  psiPackage="com.example.sumlang.psi"
  psiImplPackage="com.example.sumlang.psi.impl"

  elementTypeHolderClass="com.example.sumlang.psi.SumTypes"
  elementTypeClass="com.example.sumlang.psi.SumElementType"
  tokenTypeClass="com.example.sumlang.psi.SumTokenType"

  psiImplUtilClass="com.example.sumlang.psi.SumPsiImplUtil"
  tokens = [
    PLUS = '+'
    MINUS = '-'
    MUL = '*'
    DIV = '/'
    OPEN_B = '('
    CLOSE_B = ')'
    EQ = '='
    SEMI=';'
    IDENTIFIER="regexp:\p{Alpha}\w*"
    DIGIT="regexp:\d+"
  ]
  extends(".*expr")=expr
}

simpleFile ::= item_ *

private item_ ::= assignment ';'

assignment ::= IDENTIFIER EQ expr {
    pin=2
    recoverWhile=recover_
    mixin="com.example.sumlang.psi.impl.SumNamedElementImpl"
    extends="com.example.sumlang.psi.SumNamedElement"
    methods=[getVarName getName setName getNameIdentifier]
}

expr ::=  add_group | mul_group | primary_group {
    methods=[getValue]
}

private add_group ::= add_expr | minus_expr
private mul_group ::= mul_expr | div_expr
private primary_group ::= literal_expr | id_expr | paren_expr

add_expr ::= expr PLUS expr {
    implements=["com.example.sumlang.psi.BinaryOp"]
    methods=[getValue]
}
minus_expr ::= expr MINUS expr {
   implements=["com.example.sumlang.psi.BinaryOp"]
   methods=[getValue]
}
mul_expr ::= expr MUL expr {
   implements=["com.example.sumlang.psi.BinaryOp"]
   methods=[getValue]
}
div_expr ::= expr DIV expr {
   implements=["com.example.sumlang.psi.BinaryOp"]
   methods=[getValue]
}
literal_expr ::= DIGIT {
    methods=[getValue]
}
id_expr ::= IDENTIFIER {
    methods=[getValue getReferences]
}
paren_expr ::= OPEN_B expr CLOSE_B {
    methods=[getValue]
}

private recover_ ::= !(SEMI | (IDENTIFIER EQ))